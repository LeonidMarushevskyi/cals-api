buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

plugins {
    id 'net.saliman.cobertura' version '2.4.0'
    id "org.sonarqube" version "2.2.1"
}

project.ext {
    projectGroup = 'gov.ca.cwds.cals'
    projectDockerGroup = 'cwds'
    projectDockerAppName = 'cals-api'
    projectVersion = '1.0-SNAPSHOT'
    dropwizardVersion = '1.1.0'
    coreApiVersion = '0.20.173'
    mainClass = 'gov.ca.cwds.cals.CalsApiApplication'
}

group projectGroup
version projectVersion

repositories {
    mavenCentral()
    maven {
        url "http://maven2.cwds.tabordasolutions.net"
    }
}

sourceCompatibility = 1.8
apply plugin: "cobertura"
apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'docker'
apply plugin: 'com.github.johnrengelman.shadow'

dependencies {

    compile fileTree(dir: 'lib', include: ['*.jar'])

// Dropwizard-guice 1.0.0.2 conflicts with jersey-client 2.25.
// Exclude transitive dependencies.
    compile ('gov.ca.cwds.api:api-core:' + coreApiVersion) {
        exclude group: 'org.glassfish.jersey.core'
    }

    compile (group: 'gov.ca.cwds.cals', name: 'fas-model', version: calsModelVersion)
    compile (group: 'gov.ca.cwds.cals', name: 'cwscms-model', version: calsModelVersion)

    compile group: 'org.bouncycastle', name: 'bcprov-jdk16', version: '1.46'
    compile group: 'org.slf4j', name: 'slf4j-ext', version: '1.7.22'

    compile (
            'io.dropwizard:dropwizard-core:' + dropwizardVersion,
            'io.dropwizard:dropwizard-hibernate:' + dropwizardVersion,
            'org.postgresql:postgresql:9.4.1208.jre7',
            'io.dropwizard:dropwizard-testing:' + dropwizardVersion
    )

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'org.mockito:mockito-core:2.0.54-beta'
    testCompile group: 'org.hamcrest', name: 'hamcrest-junit', version: '2.0.0.0'
    testCompile('org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2:2.23.2') {
        exclude group: 'javax.servlet', module: 'javax.servlet-api'
        exclude group: 'junit', module: 'junit'
    }
}

mainClassName = mainClass

shadowJar {
    classifier = 'dist'
    baseName = 'cals-api'
    mergeServiceFiles()
    manifest {
        attributes 'Main-Class': mainClassName
    }
    version = ''
}

run {
    args 'server', './config/cals-api.yml'
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

def dockerTag = projectVersion + ((System.properties.getProperty('build') != null ) ? '.' + System.properties.getProperty('build') : '')

task copyConfig(type: Copy) {
    from 'config'
    into 'build/docker/config'
}

task copyDist(type: Copy, dependsOn: shadowJar) {
    from 'build/libs'
    into 'build/docker/libs'
}

task calsApiDocker(type: Docker,  dependsOn: [copyConfig, copyDist] ) {
    dockerfile = "Dockerfile"
    project.group = projectDockerGroup
    applicationName = projectDockerAppName
    tagVersion = version + ((System.properties.getProperty('build') != null ) ? '.' + System.properties.getProperty('build') : '')
}

task createDockerImage(type:Exec, dependsOn: shadowJar) {
    commandLine 'docker',  'build',  '-t',  'cwds/cals-api', '.'
}

task tagDockerImage(type:Exec, dependsOn: createDockerImage) {
    commandLine 'docker',  'tag',  'cwds/cals-api',  "cwds/cals-api:$dockerTag"
}

task pushDockerLatest(type:Exec, dependsOn: createDockerImage) {
    commandLine 'docker',  'push',  'cwds/cals-api:latest'
}

task pushDockerVersionTagged(type:Exec, dependsOn: tagDockerImage) {
    commandLine 'docker',  'push',  "cwds/cals-api:$dockerTag"
}

task publishDocker(dependsOn: [pushDockerVersionTagged, pushDockerLatest]) {
    doLast {
        println 'Published'
    }
}



